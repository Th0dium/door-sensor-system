/*
  Banlinhkien.com 
  PRJ03: DIY H·ªá Th·ªëng Ch·ªëng Tr·ªôm B√°o ƒê·ªông
  T√≠nh nƒÉng:
    - Ph√°t hi·ªán m·ªü c·ª≠a t·ª©c th·ªùi: S·ª≠ d·ª•ng c·∫£m bi·∫øn t·ª´ (c·∫£m bi·∫øn m·ªü c·ª≠a) ƒë·ªÉ ph√°t hi·ªán khi c·ª≠a b·ªã m·ªü tr√°i ph√©p.
    - C·∫£nh b√°o √¢m thanh kh·∫©n c·∫•p: Khi ph√°t hi·ªán c√≥ x√¢m nh·∫≠p tr√°i ph√©p, h·ªá th·ªëng k√≠ch ho·∫°t c√≤i b√°o ƒë·ªông ƒë·ªÉ ph√°t ra √¢m thanh c·∫£nh b√°o l·ªõn.
    - Ph√°t √¢m thanh gi·ªçng n√≥i: T√≠ch h·ª£p module JQ6500 gi√∫p ph√°t ra c√°c ƒëo·∫°n √¢m thanh c·∫£nh b√°o th·ª±c t·∫ø.
    - C·∫≠p nh·∫≠t l·ªãch s·ª≠: Hi·ªÉn th·ªã l·ªãch s·ª≠ ƒë√≥ng m·ªü c·ª≠a tr√™n giao ƒëi·ªán web, d·ªÖ dang qu·∫£n l√Ω v√† theo d√µi.
    - Email th√¥ng b√°o: Khi c√≥ c√≥ x√¢m nh·∫≠p tr√°i ph√©p h·ªá th·ªëng g·ª≠i tin nh·∫Øn c·∫£nh b√°o v·ªÅ email ng∆∞·ªùi d√πng.
  ƒê·∫•u n·ªëi:
  --------------------------------
  ESP32-C3        |        C·∫£m bi·∫øn m·ªü c·ª≠a
  GND             |           Pin 1  
  2               |           Pin 2 
  ---------------------------------
  ESP32-C3        |            N√∫t nh·∫•n SWITCH (nh·∫•n nhanh: chuy·ªÉn ch·∫ø ƒë·ªô. Nh·∫•n gi·ªØ: x√≥a c√†i ƒë·∫∑t wifi, esp s·∫Ω ph√°t ra wifi "ESP32 BLK". b·∫°n k·∫øt n·ªëi v√†o wifi, truy c·∫≠p 192.168.4.1 ƒë·ªÉ c√†i l·∫°i wifi)
  GND             |            Ch√¢n 1
  D9              |            Ch√¢n 2
  ---------------------------------
  ESP32-C3        |           C√íI
  3.3V            |           Ch√¢n +
  8               |           Ch√¢n -
  --------------------------------
  JQ6500          |           Loa 1W8R
  SPK+            |           SPK+
  SPK-            |           SPK-
  --------------------------------
  ESP32-C3        |         JQ6500
  20              |           RX
  21              |           TX
  VCC             |           5V
  GND             |           GND
  --------------------------------
  L∆∞u √Ω khi n·∫°p code:
   + Ch·ªçn Board: ESP32C3 Dev Module
   + Ch·ªçn Port : Port t∆∞∆°ng ·ª©ng
   + USB CDC On BOOT : Enable
   + Patition Scheme: Huge App (3MB No OTA/ 1Mb SPIFFS)
  Ch·ªçn ƒë∆∞·ªùng d·∫´n th∆∞ vi·ªán:
    V√†o File -> Preferences -> Sketchbook location -> C√°c b·∫°n tr·ªè ƒë·∫øn th∆∞ m·ª•c ch·ª©a th∆∞ m·ª•c BLKLab_Thu_Vien trong t√†i li·ªáu
    Ch√∫ √Ω ƒë∆∞·ªùng d·∫´n kh√¥ng ƒë∆∞·ª£c c√≥ d·∫•u ti·∫øng vi·ªát, kh√¥ng c√≥ k√≠ ƒë·∫∑c bi·ªát.
*/
#include <WiFiClient.h>
#include <ESPAsyncWebServer.h>
#include <AsyncTCP.h>
#include <EEPROM.h>
#include <Arduino_JSON.h>
#include <WiFi.h>
#include <time.h>
#include <NTPClient.h>
#include <WiFiUdp.h>
#include <Preferences.h>
#include <ESP_Mail_Client.h>
#include <WiFiClientSecure.h>

#include "index_html.h"
#include "mybutton.h"
#include "JQ6500.h"
#include "data_config.h"

Preferences preferences;
WiFiUDP ntpUDP;
NTPClient timeClient(ntpUDP, "pool.ntp.org", 7 * 3600, 60000); // GMT+7
AsyncWebServer server(80);
AsyncWebSocket ws("/ws");

#define WIFI_OK 1
#define WIFI_DIS 0
int wifi_status = WIFI_DIS;
// IPAddress local_IP(192, 168, 1, 36);   // T·∫Øt IP tƒ©nh, d√πng DHCP
// IPAddress gateway(192, 168, 1, 1);
// IPAddress subnet(255, 255, 255, 0);
// IPAddress primaryDNS(8, 8, 8, 8);
// IPAddress secondaryDNS(8, 8, 4, 4);

#define BUZZER_PIN 8
#define BUTTON_SET_PIN 9
#define BUTTON_SET_ID 1
#define SENSOR_PIN 2

Button buttonSET;
JQ6500 jq6500;

struct AlertTimer {
    int startHour;
    int startMinute;
    int endHour;
    int endMinute;
};

struct DeviceConfig {
    bool alertMode;
    AlertTimer timers[10];
    int timerCount;
    int volume;
    String alertSound;
    String normalSound;
    bool buzzerEnabled;
    bool manualOverride;
} deviceConfig;

struct EmailConfig {
    String senderEmail;
    String senderPassword;
    String recipientEmail;
    String alertContent;
} emailConfig;

struct HistoryEntry {
    String time;   // HH:MM
    String date;   // DD-MM-YYYY
    String status;
    bool alertMode;
};

const int MAX_HISTORY = 100;
HistoryEntry history[MAX_HISTORY];
int historyCount = 0;
int openOnCount = 0;
int openOffCount = 0;
String lastDate = "";

bool alarmActive = false;
unsigned long lastAudioPlayTime = 0;
const unsigned long audioPlayInterval = 5000;
bool doorOpen = false;
bool lastDoorState = false;
bool emailSent = false;

SMTPSession smtp;
void smtpCallback(SMTP_Status status);

void setup() {
    Serial.begin(115200);
    Serial.println("üîÑ Kh·ªüi ƒë·ªông h·ªá th·ªëng....");
    EEPROM.begin(512);
    readEEPROM();
    loadConfig();
    loadEmailConfig();
    loadHistory();
    loadOpenCounts();

    jq6500.begin();
    Serial.println("üîÑ JQ6500 UART Initialized");
    delay(1000);

    // if (!WiFi.config(local_IP, gateway, subnet, primaryDNS, secondaryDNS)) {
    //     Serial.println("Kh√¥ng th·ªÉ c·∫•u h√¨nh ƒë·ªãa ch·ªâ IP tƒ©nh!");
    // }

    pinMode(BUTTON_SET_PIN, INPUT_PULLUP);
    button_init(&buttonSET, BUTTON_SET_PIN, BUTTON_ACTIVE_LOW, BUTTON_SET_ID);
    button_pressshort_set_callback((void *)button_press_short_callback);
    button_presslong_set_callback((void *)button_press_long_callback);

    pinMode(BUZZER_PIN, OUTPUT);
    digitalWrite(BUZZER_PIN, HIGH);
    pinMode(SENSOR_PIN, INPUT);

    connectToWiFi();
    serverBegin(wifi_status);
    // testSMTPConnection();  // T·∫Øt t·∫°m ƒë·ªÉ tr√°nh crash
    jq6500.playFileByIndex(7);
    jq6500.play();
    // sendEmailHello();  // T·∫Øt t·∫°m ƒë·ªÉ tr√°nh crash
    beepBuzzer(500, 3);

    timeClient.update();
    lastDate = getCurrentDate();
}

void loop() {
    handle_button(&buttonSET);
    checkAlertTimers();
    openDoor();
    ws.cleanupClients();
}

//---------------------- H√†m ki·ªÉm tra b·∫≠t t·∫Øt c·∫£nh b√°o theo th·ªùi gian------------------
void checkAlertTimers() {
    if (deviceConfig.timerCount <= 0) return;
    if (deviceConfig.manualOverride) return;

    timeClient.update();
    int currentHour = timeClient.getHours();
    int currentMinute = timeClient.getMinutes();
    int currentTime = currentHour * 60 + currentMinute;

    bool shouldBeAlert = false;
    for (int i = 0; i < deviceConfig.timerCount; i++) {
        int startTime = deviceConfig.timers[i].startHour * 60 + deviceConfig.timers[i].startMinute;
        int endTime = deviceConfig.timers[i].endHour * 60 + deviceConfig.timers[i].endMinute;
        if (startTime <= endTime) {
            if (currentTime >= startTime && currentTime <= endTime) {
                shouldBeAlert = true;
                break;
            }
        } else {
            if (currentTime >= startTime || currentTime <= endTime) {
                shouldBeAlert = true;
                break;
            }
        }
    }
    if (shouldBeAlert && !deviceConfig.alertMode) {
        deviceConfig.alertMode = true;
        Serial.println("T·ª± ƒë·ªông b·∫≠t ch·∫ø ƒë·ªô c·∫£nh b√°o theo khung gi·ªù");
        saveConfig(); // L∆∞u v√†o Preferences
        ws.textAll(createConfigJSON());
    } else if (!shouldBeAlert && deviceConfig.alertMode) {
        deviceConfig.alertMode = false;
        Serial.println("T·ª± ƒë·ªông t·∫Øt ch·∫ø ƒë·ªô c·∫£nh b√°o theo khung gi·ªù");
        saveConfig(); // L∆∞u v√†o Preferences
        ws.textAll(createConfigJSON());
    }
}

#define DEBOUNCE_TIME 100
unsigned long lastDebounceTime = 0;
bool lastStableDoorState = LOW;

void openDoor() {
    bool currentRawState = digitalRead(SENSOR_PIN);
    if (currentRawState != lastStableDoorState) {
        lastDebounceTime = millis();
    }
    if ((millis() - lastDebounceTime) > DEBOUNCE_TIME) {
        if (currentRawState != lastDoorState) {
            lastDoorState = currentRawState;
            timeClient.update();
            String currentTime = timeClient.getFormattedTime().substring(0, 5);
            String currentDate = getCurrentDate();

            if (currentRawState == LOW && lastDate != "" && currentDate != lastDate) {
                clearHistory();
                openOnCount = 0;
                openOffCount = 0;
                saveOpenCounts();
                lastDate = currentDate;
                Serial.println("Ng√†y m·ªõi: " + currentDate + " - ƒê√£ x√≥a d·ªØ li·ªáu ng√†y c≈©");
                ws.textAll(createHistoryJSON());
            }

            if (currentRawState == LOW) {
                doorOpen = true;
                Serial.println("C·ª≠a m·ªü");
                addHistoryEntry(currentTime, currentDate, "M·ªü", deviceConfig.alertMode);
                if (deviceConfig.alertMode) {
                    openOnCount++;
                    alarmActive = true;
                    beepBuzzer(100, 6);
                    playAlertSound();
                    // if (!emailSent) {
                    //     sendEmail();  // T·∫Øt t·∫°m ƒë·ªÉ tr√°nh crash
                    //     emailSent = true;
                    // }
                } else {
                    openOffCount++;
                    jq6500.resetMP3();
                    playNormalSound();
                }
                saveOpenCounts();
            } else {
                doorOpen = false;
                Serial.println("C·ª≠a ƒë√≥ng");
                addHistoryEntry(currentTime, currentDate, "ƒê√≥ng", deviceConfig.alertMode);
                if (deviceConfig.alertMode) {
                    alarmActive = false;
                }
                emailSent = false;
                jq6500.resetMP3();
            }
            lastDate = currentDate;
            ws.textAll(createHistoryJSON());
            ws.textAll(createConfigJSON());
        }
    }
    lastStableDoorState = currentRawState;
    if (deviceConfig.alertMode && alarmActive && doorOpen) {
        if (millis() - lastAudioPlayTime >= audioPlayInterval) {
            playAlertSound();
            lastAudioPlayTime = millis();
            beepBuzzer(100, 6);
        }
        if (deviceConfig.buzzerEnabled) {
            beepBuzzer(100, 10);
            jq6500.resetMP3();
        }
    }
}

String getCurrentDate() {
    timeClient.update();
    time_t epochTime = timeClient.getEpochTime();
    struct tm *ptm = gmtime(&epochTime);
    char dateBuffer[11];
    sprintf(dateBuffer, "%02d-%02d-%04d", ptm->tm_mday, ptm->tm_mon + 1, ptm->tm_year + 1900);
    return String(dateBuffer);
}

//--------------------H√†m ph√°t c√≤i ch√≠p-----------------
void beepBuzzer(int delayBeep, int timeBeep) {
    for (int i = 0; i < timeBeep; i++) {
        digitalWrite(BUZZER_PIN, LOW);
        delay(delayBeep);
        digitalWrite(BUZZER_PIN, HIGH);
        delay(delayBeep);
    }
}

//----------------H√†m ph√°t √¢m thanh t·∫Øt c·∫£nh b√°o---------------
void playAlertSound() {
    if (deviceConfig.alertSound != "none") {
        jq6500.setVolume(deviceConfig.volume);
        if (deviceConfig.alertSound == "alert1") jq6500.playFileByIndex(1);
        else if (deviceConfig.alertSound == "alert2") jq6500.playFileByIndex(2);
        else if (deviceConfig.alertSound == "alert3") jq6500.playFileByIndex(3);
        jq6500.play();
        Serial.println("Ph√°t √¢m thanh c·∫£nh b√°o: " + deviceConfig.alertSound);
    }
}

//----------------H√†m ph√°t √¢m thanh c·∫£nh b√°o---------------
void playNormalSound() {
    if (deviceConfig.normalSound != "none") {
        jq6500.setVolume(deviceConfig.volume);
        if (deviceConfig.normalSound == "normal1") jq6500.playFileByIndex(4);
        else if (deviceConfig.normalSound == "normal2") jq6500.playFileByIndex(5);
        else if (deviceConfig.normalSound == "normal3") jq6500.playFileByIndex(6);
        jq6500.play();
        Serial.println("Ph√°t √¢m thanh b√¨nh th∆∞·ªùng: " + deviceConfig.normalSound);
    }
}

// -----------------------H√†m th√™m l·ªãch s·ª≠---------------------
void addHistoryEntry(String time, String date, String status, bool alertMode) {
    if (historyCount >= MAX_HISTORY) {
        for (int i = 0; i < MAX_HISTORY - 1; i++) {
            history[i] = history[i + 1];
        }
        history[MAX_HISTORY - 1].time = time;
        history[MAX_HISTORY - 1].date = date;
        history[MAX_HISTORY - 1].status = status;
        history[MAX_HISTORY - 1].alertMode = alertMode;
    } else {
        for (int i = historyCount; i > 0; i--) {
            history[i] = history[i - 1];
        }
        history[0].time = time;
        history[0].date = date;
        history[0].status = status;
        history[0].alertMode = alertMode;
        historyCount++;
    }
    saveHistory();
    Serial.println("Th√™m l·ªãch s·ª≠ - Ng√†y: " + date + ", Th·ªùi gian: " + time + ", Tr·∫°ng th√°i: " + status + ", AlertMode: " + String(alertMode));
}

// ------------------------ H√†m l∆∞u l·∫°i l·ªãch s·ª≠-----------------
void saveHistory() {
    preferences.begin("history", false);
    preferences.putInt("historyCount", historyCount);
    for (int i = 0; i < historyCount; i++) {
        String keyTime = "history" + String(i) + "_time";
        String keyDate = "history" + String(i) + "_date";
        String keyStatus = "history" + String(i) + "_status";
        String keyAlertMode = "history" + String(i) + "_alertMode";
        preferences.putString(keyTime.c_str(), history[i].time);
        preferences.putString(keyDate.c_str(), history[i].date);
        preferences.putString(keyStatus.c_str(), history[i].status);
        preferences.putBool(keyAlertMode.c_str(), history[i].alertMode);
    }
    preferences.end();
    Serial.println("ƒê√£ l∆∞u l·ªãch s·ª≠ v√†o Preferences");
}

//-----------------------------H√†m t·∫£i l·∫°i l·ªãch s·ª≠ khi kh·ªüi ƒë·ªông-----------------
void loadHistory() {
    preferences.begin("history", true);
    historyCount = preferences.getInt("historyCount", 0);
    for (int i = 0; i < historyCount; i++) {
        String keyTime = "history" + String(i) + "_time";
        String keyDate = "history" + String(i) + "_date";
        String keyStatus = "history" + String(i) + "_status";
        String keyAlertMode = "history" + String(i) + "_alertMode";
        history[i].time = preferences.getString(keyTime.c_str(), "");
        history[i].date = preferences.getString(keyDate.c_str(), "");
        history[i].status = preferences.getString(keyStatus.c_str(), "");
        history[i].alertMode = preferences.getBool(keyAlertMode.c_str(), false);
    }
    preferences.end();
    Serial.println("ƒê√£ n·∫°p l·ªãch s·ª≠ t·ª´ Preferences");
}

void saveOpenCounts() {
    preferences.begin("openCounts", false);
    preferences.putInt("openOnCount", openOnCount);
    preferences.putInt("openOffCount", openOffCount);
    preferences.putString("lastDate", lastDate);
    preferences.end();
    Serial.println("ƒê√£ l∆∞u s·ªë l·∫ßn m·ªü - OpenOn: " + String(openOnCount) + ", OpenOff: " + String(openOffCount) + ", LastDate: " + lastDate);
}

void loadOpenCounts() {
    preferences.begin("openCounts", true);
    openOnCount = preferences.getInt("openOnCount", 0);
    openOffCount = preferences.getInt("openOffCount", 0);
    lastDate = preferences.getString("lastDate", "");
    preferences.end();
    Serial.println("ƒê√£ n·∫°p s·ªë l·∫ßn m·ªü - OpenOn: " + String(openOnCount) + ", OpenOff: " + String(openOffCount) + ", LastDate: " + lastDate);
}
// ------------------H√†m x√≥a l·ªãch s·ª≠--------------------
void clearHistory() {
    preferences.begin("history", false);
    preferences.clear();
    preferences.putInt("historyCount", 0);
    preferences.end();
    historyCount = 0;
    Serial.println("ƒê√£ x√≥a to√†n b·ªô l·ªãch s·ª≠ trong Preferences");
    ws.textAll(createHistoryJSON()); // G·ª≠i l·ªãch s·ª≠ r·ªóng sau khi x√≥a
}

//--------------------H√†m ki·ªÉm tra k·∫øt n·ªëi Email-----------------
void testSMTPConnection() {
    WiFiClientSecure client;
    client.setInsecure();
    Serial.print("ƒêang k·∫øt n·ªëi t·ªõi SMTP Server...");
    if (client.connect("smtp.gmail.com", 465)) {
        Serial.println(" K·∫øt n·ªëi th√†nh c√¥ng!");
        beepBuzzer(100, 4);
    } else {
        Serial.println(" Kh√¥ng k·∫øt n·ªëi ƒë∆∞·ª£c!");
        beepBuzzer(100, 5);
    }
}

//------------------- H√†m g·ª≠i tin nh·∫Øn Email---------------------
void sendEmail() {
    if (WiFi.status() == WL_CONNECTED) {
        MailClient.networkReconnect(true);
        smtp.debug(1);
        smtp.callback(smtpCallback);

        Session_Config config;
        config.server.host_name = "smtp.gmail.com";
        config.server.port = 465;
        config.login.email = emailConfig.senderEmail;
        config.login.password = emailConfig.senderPassword;
        config.login.user_domain = "";

        config.time.ntp_server = F("pool.ntp.org,time.nist.gov");
        config.time.gmt_offset = 7;
        config.time.day_light_offset = 0;

        SMTP_Message message;
        message.sender.name = F("H·ªá Th·ªëng Ch·ªëng Tr·ªôm ESP32 Maker BLK");
        message.sender.email = emailConfig.senderEmail;
        message.subject = F("‚ö†Ô∏è C·∫£nh B√°o");
        message.addRecipient(F("BLK"), emailConfig.recipientEmail);

        String textMsg = emailConfig.alertContent;
        message.text.content = textMsg.c_str();
        message.text.charSet = "us-ascii";
        message.text.transfer_encoding = Content_Transfer_Encoding::enc_7bit;

        message.priority = esp_mail_smtp_priority::esp_mail_smtp_priority_low;
        message.response.notify = esp_mail_smtp_notify_success | esp_mail_smtp_notify_failure | esp_mail_smtp_notify_delay;

        if (!smtp.connect(&config)) {
            ESP_MAIL_PRINTF("L·ªói k·∫øt n·ªëi, M√£ tr·∫°ng th√°i: %d, M√£ l·ªói: %d, L√Ω do: %s", smtp.statusCode(), smtp.errorCode(), smtp.errorReason().c_str());
            return;
        }

        if (!MailClient.sendMail(&smtp, &message))
            ESP_MAIL_PRINTF("M√£ tr·∫°ng th√°i: %d, M√£ l·ªói: %d, L√Ω do: %s", smtp.statusCode(), smtp.errorCode(), smtp.errorReason().c_str());
    }
}

//------------------------H√†m g·ª≠i tin nh·∫Øn Email khi kh·ªüi ƒë·ªông-----------------------
void sendEmailHello() {
    if (WiFi.status() == WL_CONNECTED) {
        MailClient.networkReconnect(true);
        smtp.debug(1);
        smtp.callback(smtpCallback);

        Session_Config config;
        config.server.host_name = "smtp.gmail.com";
        config.server.port = 465;
        config.login.email = emailConfig.senderEmail;
        config.login.password = emailConfig.senderPassword;
        config.login.user_domain = "";

        config.time.ntp_server = F("pool.ntp.org,time.nist.gov");
        config.time.gmt_offset = 7;
        config.time.day_light_offset = 0;

        SMTP_Message message;
        message.sender.name = F("H·ªá Th·ªëng Ch·ªëng Tr·ªôm ESP32 Maker BLK");
        message.sender.email = emailConfig.senderEmail;
        message.subject = F("üîí Xin Ch√†o üîí");
        message.addRecipient(F("BLK"), emailConfig.recipientEmail);

        String textMsg = "Xin ch√†o, b·∫°n ƒë√£ kh·ªüi ƒë·ªông h·ªá th·ªëng ch·ªëng tr·ªôm th√¥ng minh";
        message.text.content = textMsg.c_str();
        message.text.charSet = "us-ascii";
        message.text.transfer_encoding = Content_Transfer_Encoding::enc_7bit;

        if (!smtp.connect(&config)) {
            ESP_MAIL_PRINTF("L·ªói k·∫øt n·ªëi, M√£ tr·∫°ng th√°i: %d, M√£ l·ªói: %d, L√Ω do: %s", smtp.statusCode(), smtp.errorCode(), smtp.errorReason().c_str());
            return;
        }

        if (!MailClient.sendMail(&smtp, &message))
            ESP_MAIL_PRINTF("M√£ tr·∫°ng th√°i: %d, M√£ l·ªói: %d, L√Ω do: %s", smtp.statusCode(), smtp.errorCode(), smtp.errorReason().c_str());
    }
}

void smtpCallback(SMTP_Status status) {
    Serial.println(status.info());
    if (status.success()) {
        Serial.println("----------------");
        ESP_MAIL_PRINTF("Message sent success: %d\n", status.completedCount());
        ESP_MAIL_PRINTF("Message sent failed: %d\n", status.failedCount());
        Serial.println("----------------\n");

        for (size_t i = 0; i < smtp.sendingResult.size(); i++) {
            SMTP_Result result = smtp.sendingResult.getItem(i);
            ESP_MAIL_PRINTF("Message No: %d\n", i + 1);
            ESP_MAIL_PRINTF("Status: %s\n", result.completed ? "success" : "failed");
            ESP_MAIL_PRINTF("Date/Time: %s\n", MailClient.Time.getDateTimeString(result.timestamp, "%B %d, %Y %H:%M:%S").c_str());
            ESP_MAIL_PRINTF("Recipient: %s\n", result.recipients.c_str());
            ESP_MAIL_PRINTF("Subject: %s\n", result.subject.c_str());
        }
        Serial.println("----------------\n");
        smtp.sendingResult.clear();
    }
}

//------------------------C√°c h√†m l∆∞u v√† t·∫£i c√°c c√†i ƒë·∫∑t ƒë∆∞·ª£c thi·∫øt l·∫≠p------------------
void saveConfig() {
    preferences.begin("deviceConfig", false);
    preferences.putBool("alertMode", deviceConfig.alertMode);
    preferences.putInt("timerCount", deviceConfig.timerCount);
    for (int i = 0; i < deviceConfig.timerCount; i++) {
        String keyStartHour = "timer" + String(i) + "_startHour";
        String keyStartMinute = "timer" + String(i) + "_startMinute";
        String keyEndHour = "timer" + String(i) + "_endHour";
        String keyEndMinute = "timer" + String(i) + "_endMinute";
        preferences.putInt(keyStartHour.c_str(), deviceConfig.timers[i].startHour);
        preferences.putInt(keyStartMinute.c_str(), deviceConfig.timers[i].startMinute);
        preferences.putInt(keyEndHour.c_str(), deviceConfig.timers[i].endHour);
        preferences.putInt(keyEndMinute.c_str(), deviceConfig.timers[i].endMinute);
    }
    preferences.putInt("volume", deviceConfig.volume);
    preferences.putString("alertSound", deviceConfig.alertSound);
    preferences.putString("normalSound", deviceConfig.normalSound);
    preferences.putBool("buzzerEnabled", deviceConfig.buzzerEnabled);
    preferences.putBool("manualOverride", deviceConfig.manualOverride);
    preferences.end();
    Serial.println("ƒê√£ l∆∞u c·∫•u h√¨nh v√†o Preferences");
}

void loadConfig() {
    preferences.begin("deviceConfig", true);
    deviceConfig.alertMode = preferences.getBool("alertMode", false);
    deviceConfig.timerCount = preferences.getInt("timerCount", 0);
    for (int i = 0; i < deviceConfig.timerCount; i++) {
        String keyStartHour = "timer" + String(i) + "_startHour";
        String keyStartMinute = "timer" + String(i) + "_startMinute";
        String keyEndHour = "timer" + String(i) + "_endHour";
        String keyEndMinute = "timer" + String(i) + "_endMinute";
        deviceConfig.timers[i].startHour = preferences.getInt(keyStartHour.c_str(), 0);
        deviceConfig.timers[i].startMinute = preferences.getInt(keyStartMinute.c_str(), 0);
        deviceConfig.timers[i].endHour = preferences.getInt(keyEndHour.c_str(), 0);
        deviceConfig.timers[i].endMinute = preferences.getInt(keyEndMinute.c_str(), 0);
    }
    deviceConfig.volume = preferences.getInt("volume", 50);
    deviceConfig.alertSound = preferences.getString("alertSound", "none");
    deviceConfig.normalSound = preferences.getString("normalSound", "none");
    deviceConfig.buzzerEnabled = preferences.getBool("buzzerEnabled", false);
    deviceConfig.manualOverride = preferences.getBool("manualOverride", false);
    preferences.end();
    Serial.println("ƒê√£ n·∫°p c·∫•u h√¨nh t·ª´ Preferences");
}

void loadEmailConfig() {
    preferences.begin("emailConfig", true);
    emailConfig.senderEmail = preferences.getString("senderEmail", "");
    emailConfig.senderPassword = preferences.getString("senderPassword", "");
    emailConfig.recipientEmail = preferences.getString("recipientEmail", "");
    emailConfig.alertContent = preferences.getString("alertContent", "C·∫£nh b√°o: C·ª≠a ƒë√£ ƒë∆∞·ª£c m·ªü!");
    preferences.end();
    Serial.println("ƒê√£ n·∫°p c·∫•u h√¨nh email t·ª´ Preferences");
}

//-------------------- H√†m ki·ªÉm tra k·∫øt n·ªëi Wifi---------------------------
void connectToWiFi() {
    if (Essid.length() > 1) {
        Serial.println(Essid);
        Serial.println(Epass);
        WiFi.begin(Essid.c_str(), Epass.c_str());
        WiFi.mode(WIFI_STA);
        int countConnect = 0;
        while (WiFi.status() != WL_CONNECTED) {
            delay(500);
            Serial.print(".");
            if (countConnect++ == 10) {
                Serial.println("\nK·∫øt n·ªëi WiFi th·∫•t b·∫°i");
                beepBuzzer(100, 5);
                wifi_status = WIFI_DIS;
                break;
            }
        }
        if (WiFi.status() == WL_CONNECTED) {
            beepBuzzer(100, 3);
            Serial.println("\nƒê√£ k·∫øt n·ªëi WiFi");
            Serial.println("IP: " + WiFi.localIP().toString());
            timeClient.begin();
            wifi_status = WIFI_OK;
        }
    }
}

// -------------------Kh·ªüi ƒë·ªông server---------------------------
void serverBegin(int wifi_status) {
    server.end();
    delay(100);
    if (wifi_status == WIFI_OK) {
        server.on("/", HTTP_GET, [](AsyncWebServerRequest *request) {
            request->send_P(200, "text/html", index_html_stamode);
        });

        ws.onEvent(onWsEvent);
        server.addHandler(&ws);
        server.begin();
        Serial.println("Server STA mode v·ªõi WebSocket ƒë√£ kh·ªüi ƒë·ªông");
    } else if (wifi_status == WIFI_DIS) {
        Serial.println("WIFI_DIS_APMODE");
        WiFi.softAP(ssidAP, passwordAP);
        server.on("/", HTTP_GET, [](AsyncWebServerRequest *request) {
            request->send_P(200, "text/html", index_html_apmode);
        });
        server.on("/data_before", HTTP_GET, [](AsyncWebServerRequest *request) {
            String json = getJsonData();
            request->send(200, "application/json", json);
            Serial.println("G·ª≠i d·ªØ li·ªáu WiFi AP: " + json);
        });
        server.on("/post_data", HTTP_POST, [](AsyncWebServerRequest *request) {
            request->send(200, "text/plain", "SUCCESS");
            ESP.restart();
        }, NULL, getDataFromClient);
        server.begin();
        Serial.println("Server AP mode ƒë√£ kh·ªüi ƒë·ªông");
    }
}

//--------------- X·ª≠ l√Ω s·ª± ki·ªán WebSocket----------------------
void onWsEvent(AsyncWebSocket *server, AsyncWebSocketClient *client, AwsEventType type, void *arg, uint8_t *data, size_t len) {
    if (type == WS_EVT_CONNECT) {
        Serial.println("WebSocket client connected");
        String configData = createConfigJSON();
        String historyData = createHistoryJSON();
        String emailData = createEmailConfigJSON();
        client->text("{\"type\":\"config\",\"data\":" + configData + "}");
        client->text("{\"type\":\"history\",\"data\":" + historyData + "}");
        client->text("{\"type\":\"email\",\"data\":" + emailData + "}");
        Serial.println("Sent initial data to WebSocket client");
    } else if (type == WS_EVT_DISCONNECT) {
        Serial.println("WebSocket client disconnected");
    } else if (type == WS_EVT_DATA) {
        String msg = "";
        for (size_t i = 0; i < len; i++) {
            msg += (char)data[i];
        }
        Serial.println("Received from WebSocket: " + msg);
        handleWebSocketMessage(msg);
    }
}

void handleWebSocketMessage(String msg) {
    JSONVar doc = JSON.parse(msg);
    if (JSON.typeof(doc) == "undefined") {
        Serial.println("‚ö†Ô∏è L·ªói ph√¢n t√≠ch JSON!");
        return;
    }

    if (doc.hasOwnProperty("type")) {
        String type = doc["type"];
        JSONVar data = doc["data"];

        if (type == "config") {
            deviceConfig.alertMode = (bool)data["alertMode"];
            deviceConfig.timerCount = (int)data["timerCount"];
            deviceConfig.volume = (int)data["volume"];
            deviceConfig.alertSound = String(data["alertSound"]);
            deviceConfig.normalSound = String(data["normalSound"]);
            deviceConfig.manualOverride = true;

            JSONVar timers = data["timers"];
            if (JSON.typeof(timers) == "array") {
                for (int i = 0; i < deviceConfig.timerCount; i++) {
                    deviceConfig.timers[i].startHour = (int)timers[i]["startHour"];
                    deviceConfig.timers[i].startMinute = (int)timers[i]["startMinute"];
                    deviceConfig.timers[i].endHour = (int)timers[i]["endHour"];
                    deviceConfig.timers[i].endMinute = (int)timers[i]["endMinute"];
                }
            }
            saveConfig(); // L∆∞u c·∫•u h√¨nh v√†o Preferences ngay khi nh·∫≠n
            printConfig();
            ws.textAll("{\"type\":\"config\",\"data\":" + createConfigJSON() + "}");
        } else if (type == "email") {
            emailConfig.senderEmail = String(data["senderEmail"]);
            emailConfig.senderPassword = String(data["senderPassword"]);
            emailConfig.recipientEmail = String(data["recipientEmail"]);
            emailConfig.alertContent = String(data["alertContent"]);
            saveEmailConfig(); // L∆∞u c·∫•u h√¨nh email v√†o Preferences ngay khi nh·∫≠n
            ws.textAll("{\"type\":\"email\",\"data\":" + createEmailConfigJSON() + "}");
        } else if (type == "clear_history") {
            clearHistory();
            openOnCount = 0;
            openOffCount = 0;
            saveOpenCounts();
            ws.textAll("{\"type\":\"history\",\"data\":" + createHistoryJSON() + "}");
        }
    }
}

void printConfig() {
    Serial.println("\n===== C·∫§U H√åNH NH·∫¨N ƒê∆Ø·ª¢C =====");
    Serial.print("üîî Ch·∫ø ƒë·ªô c·∫£nh b√°o: "); Serial.println(deviceConfig.alertMode ? "B·∫≠t" : "T·∫Øt");
    Serial.print("üìä S·ªë l∆∞·ª£ng h·∫πn gi·ªù: "); Serial.println(deviceConfig.timerCount);
    for (int i = 0; i < deviceConfig.timerCount; i++) {
        Serial.printf("üïí Timer %d: %02d:%02d ‚Üí %02d:%02d\n", i + 1,
                      deviceConfig.timers[i].startHour, deviceConfig.timers[i].startMinute,
                      deviceConfig.timers[i].endHour, deviceConfig.timers[i].endMinute);
    }
    Serial.print("üîä √Çm l∆∞·ª£ng: "); Serial.println(deviceConfig.volume);
    Serial.print("üéµ √Çm b√°o c·∫£nh b√°o: "); Serial.println(deviceConfig.alertSound);
    Serial.print("üé∂ √Çm b√°o th∆∞·ªùng: "); Serial.println(deviceConfig.normalSound);
    Serial.print("üîß Ch·∫ø ƒë·ªô th·ªß c√¥ng: "); Serial.println(deviceConfig.manualOverride ? "B·∫≠t" : "T·∫Øt");
    Serial.println("================================\n");
}

String createConfigJSON() {
    JSONVar jsonData;
    jsonData["alertMode"] = deviceConfig.alertMode;
    JSONVar timers;
    for (int i = 0; i < deviceConfig.timerCount; i++) {
        JSONVar timer;
        timer["startHour"] = deviceConfig.timers[i].startHour;
        timer["startMinute"] = deviceConfig.timers[i].startMinute;
        timer["endHour"] = deviceConfig.timers[i].endHour;
        timer["endMinute"] = deviceConfig.timers[i].endMinute;
        timers[i] = timer;
    }
    jsonData["timers"] = timers;
    jsonData["timerCount"] = deviceConfig.timerCount;
    jsonData["volume"] = deviceConfig.volume;
    jsonData["alertSound"] = deviceConfig.alertSound;
    jsonData["normalSound"] = deviceConfig.normalSound;
    jsonData["manualOverride"] = deviceConfig.manualOverride;
    String jsonString = JSON.stringify(jsonData);
    return jsonString;
}

String createHistoryJSON() {
    JSONVar jsonData;
    for (int i = 0; i < historyCount; i++) {
        JSONVar event;
        event["time"] = history[i].time;
        event["date"] = history[i].date;
        event["status"] = history[i].status;
        event["alertMode"] = history[i].alertMode;
        jsonData[i] = event;
    }
    String jsonString = JSON.stringify(jsonData);
    return jsonString;
}

String createEmailConfigJSON() {
    JSONVar jsonData;
    jsonData["senderEmail"] = emailConfig.senderEmail;
    jsonData["senderPassword"] = emailConfig.senderPassword;
    jsonData["recipientEmail"] = emailConfig.recipientEmail;
    jsonData["alertContent"] = emailConfig.alertContent;
    String jsonString = JSON.stringify(jsonData);
    return jsonString;
}

//------------------ L∆∞u c·∫•u h√¨nh email v√†o preferences------------------
void saveEmailConfig() {
    preferences.begin("emailConfig", false);
    preferences.putString("senderEmail", emailConfig.senderEmail);
    preferences.putString("senderPassword", emailConfig.senderPassword);
    preferences.putString("recipientEmail", emailConfig.recipientEmail);
    preferences.putString("alertContent", emailConfig.alertContent);
    preferences.end();
    Serial.println("ƒê√£ l∆∞u c·∫•u h√¨nh email v√†o Preferences");
}

//--------------------- X·ª≠ l√Ω n√∫t nh·∫•n ng·∫Øn-----------------------
void button_press_short_callback(uint8_t button_id) {
    if (button_id == BUTTON_SET_ID) {
        deviceConfig.alertMode = !deviceConfig.alertMode;
        deviceConfig.manualOverride = true;
        Serial.println("Ch·∫ø ƒë·ªô c·∫£nh b√°o: " + String(deviceConfig.alertMode ? "B·∫≠t" : "T·∫Øt"));
        saveConfig();
        ws.textAll("{\"type\":\"config\",\"data\":" + createConfigJSON() + "}");
    }
}

//-------------------- X·ª≠ l√Ω n√∫t nh·∫•n d√†i---------------------
void button_press_long_callback(uint8_t button_id) {
    if (button_id == BUTTON_SET_ID) {
        wifi_status = WIFI_DIS;
        clearEeprom();
        ESP.restart();
        Serial.println("ƒê√£ reset thi·∫øt b·ªã");
    }
}

//-------------------- ƒê·ªçc EEPROM-----------------------------
void readEEPROM() {
    for (int i = 0; i < 32; ++i) Essid += char(EEPROM.read(i));
    for (int i = 32; i < 64; ++i) Epass += char(EEPROM.read(i));
    if (Essid.length() == 0) Essid = "BLK";
    Serial.println("ƒê·ªçc t·ª´ EEPROM - SSID: " + Essid + ", Password: " + Epass);
}

//-------------------- X√≥a EEPROM-------------------------
void clearEeprom() {
    for (int i = 0; i < 250; ++i) EEPROM.write(i, 0);
    EEPROM.commit();
    Serial.println("ƒê√£ x√≥a EEPROM");
}

//---------------------- Ghi EEPROM---------------------------
void writeEEPROM() {
    clearEeprom();
    for (int i = 0; i < Essid.length(); ++i) EEPROM.write(i, Essid[i]);
    for (int i = 0; i < Epass.length(); ++i) EEPROM.write(32 + i, Epass[i]);
    EEPROM.commit();
    Serial.println("ƒê√£ ghi v√†o EEPROM - SSID: " + Essid + ", Password: " + Epass);
    delay(500);
}

//----------------------- D·ªØ li·ªáu WiFi AP mode-----------------------
String getJsonData() {
    JSONVar myObject;
    myObject["ssid"] = Essid;
    myObject["pass"] = Epass;
    String jsonString = JSON.stringify(myObject);
    Serial.println("D·ªØ li·ªáu WiFi AP g·ª≠i ƒëi: " + jsonString);
    return jsonString;
}

//------------------ Nh·∫≠n d·ªØ li·ªáu t·ª´ client AP mode----------------------------
void getDataFromClient(AsyncWebServerRequest *request, uint8_t *data, size_t len, size_t index, size_t total) {
    JSONVar myObject = JSON.parse((char *)data);
    if (myObject.hasOwnProperty("ssid")) Essid = (const char*)myObject["ssid"];
    if (myObject.hasOwnProperty("pass")) Epass = (const char*)myObject["pass"];
    Serial.println("Nh·∫≠n d·ªØ li·ªáu WiFi t·ª´ client - SSID: " + Essid + ", Password: " + Epass);
    writeEEPROM();
}